agent Main {
    module EIS ei;
    module Console C;
    module System S;

    types tower_ont {
        formula free(string);
    }

    inference free(string X) :- ~ei.on(string Y, X);
    inference free(string X) :- X == "table";

    rule +!main(list args) {
        ei.launch("hw","dependency/tower-1.3.0.jar");
        ei.init();
	    ei.join();
        ei.link("gripper");
	    ei.startEnv();

        C.println("waiting for block d");
        wait(ei.block("d"));

        !tower("b", "a", "d");
        !tower("a", "d", "b");
        S.exit();
    }

    rule +!tower(string X, string Y, string Z) {
        !holding(Z);!on(Z, "table");
        !holding(Y);!on(Y, Z);
        !holding(X);!on(X, Y);
    }

    rule +!holding(string target) : ~ei.holding(string Y) & free(target) {
        ei.pickup(target);
        wait(ei.holding(target));
    }

    rule +!holding(string target) : ~ei.holding(string Y) & ~free(target) {
        C.println("called holding but target is not free");
        !free(target);
    }

    rule +!free(string target) : ~free(target) & ei.on(string A, target) {
        C.println("putting the top block on the table");
        if (~free(A)) !free(A);
        !holding(A);
        !on(A, "table");
        !holding(target);
    }

    rule +!on(string block, string target) : ei.holding(block) & free(target) {
        ei.putdown(block, target);
        wait(ei.on(block, target));
    }

}
